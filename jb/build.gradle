plugins {
    id "java"
    id "idea"
    id "org.jetbrains.intellij" version "1.1.6"
    id "org.jetbrains.kotlin.jvm" version "1.6.10"
}

group "com.codestream"
version "12.11.1"

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
    maven { url = "https://europe-maven.pkg.dev/jxbrowser/releases" }
    maven { url = "https://www.jetbrains.com/intellij-repository/snapshots" }
}

ext {
    jxBrowserVersion = "7.18"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.6.10"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.5.2"

    implementation "com.teamdev.jxbrowser:jxbrowser:${jxBrowserVersion}"
    implementation "com.teamdev.jxbrowser:jxbrowser-swing:${jxBrowserVersion}"

    implementation group: "org.eclipse.lsp4j", name: "org.eclipse.lsp4j", version: "0.12.0"
    implementation "com.github.salomonbrys.kotson:kotson:2.5.0"
    implementation("io.sentry:sentry:1.7.27") {
        exclude group: 'org.slf4j', module: 'slf4j-simple'
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    implementation group: 'commons-io', name: 'commons-io', version: '2.6'
    implementation("org.reflections:reflections:0.9.12")
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

patchPluginXml {
    changeNotes = "${file('docs/change-notes.html').getText('UTF-8')}"
    pluginDescription = "${file('docs/description.html').getText('UTF-8')}"
}

idea {
    module {
        excludeDirs += file("src/main/resources/agent")
        excludeDirs += file("src/main/resources/webview")
    }
}


intellij {
    type = "IU"
    // the version parameter dictates which jars will be used at compilation time

    version = "2021.1.1" // earliest version we support

    // version "201.5616-EAP-CANDIDATE-SNAPSHOT"
    // version "2019.2.3"
    // version "2019.3"
    // version "IC-2019.2.2"
    // version "2018.3.4" // you can also specify dot dot releases
    // version "IC-2018.2.7" //
    // version "PC-2018.2"

    intellij.updateSinceUntilBuild = false
    // Run on a locally installed IDE, different from the build dependency specified via type/version
    // intellij.alternativeIdePath = idePath

    plugins = ['git4idea', 'org.jetbrains.plugins.go:211.7142.36', 'Pythonid:203.6682.179']
}

runIde {
//    jbrVersion "8u202b1483.58"
//    jbrVersion "11_0_2b159"
//    jvmArgs '-Dsun.java2d.uiScale.enabled=false'
//    jvmArgs '-Dkotlinx.coroutines.debug=on'
//    jvmArgs '-Dcom.codestream.recordRequests=true'
    jvmArgs '-Dcom.codestream.webview=/Users/mfarias/Code/codestream/jb/src/main/resources/webview'
    jvmArgs '-Dcom.codestream.agent=/Users/mfarias/Code/codestream/shared/agent/dist'
    jvmArgs '-Xmx2048m'
    jvmArgs '-Xms128m'
}

import org.gradle.internal.os.OperatingSystem

task buildDeps(type: Exec) {
    String npm = 'npm';
    if (OperatingSystem.current().isWindows()) {
        npm = 'npm.cmd'
    }

    workingDir "$projectDir"
    commandLine npm, 'run', 'bundle'
}

task buildDepsMac(type: Exec) {
    workingDir "$projectDir"
    commandLine 'npm', 'run', 'bundle:mac'
}

task copyAgent(type: Copy) {
    from "$projectDir/../shared/agent/dist"
    include "**/*"
    into "$projectDir/src/main/resources/agent"
}

task copyProtobuf(type: Copy) {
    from "$projectDir/../shared/protobuf"
    include "**/*"
    into "$projectDir/src/main/resources/protobuf"
}

task buildDependencies {}
buildDependencies.dependsOn buildDeps, copyAgent, copyProtobuf

task buildDependenciesMac {}
buildDependenciesMac.dependsOn buildDepsMac, copyAgent, copyProtobuf

buildPlugin.dependsOn buildDependencies

task buildDebugDeps(type: Exec) {
    String npm = 'npm';
    if (OperatingSystem.current().isWindows()) {
        npm = 'npm.cmd'
    }

    workingDir "$projectDir"
    commandLine npm, 'run', 'build'
}

task copyDebugAgent(type: Copy) {
    from "$projectDir/../shared/agent/dist"
    include "**/*"
    into "$projectDir/src/main/resources/agent"
}
copyDebugAgent.dependsOn buildDebugDeps

task buildDebugDependencies {}
buildDebugDependencies.dependsOn buildDebugDeps, copyDebugAgent, copyProtobuf

























